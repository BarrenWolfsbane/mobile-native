
buildscript {
    ext {
        buildToolsVersion = "28.0.2"
        minSdkVersion = 21
        compileSdkVersion = 28
        targetSdkVersion = 27
        supportLibVersion = "28.0.0"
        glideVersion = "4.7.1"
    }
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        mavenLocal()
        maven {
            // url "$rootDir/../../jitsi-maven-repository/releases" // used for local dev of jitsi sdk
            // url "https://github.com/jitsi/jitsi-maven-repository/raw/master/releases" // original
            url "https://gitlab.com/minds/jitsi-maven-repository/raw/master/releases" // ours custom sdk
        }
        google()
        jcenter()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url "$rootDir/../node_modules/react-native/android"
        }
        maven {
            // Local Maven repo containing AARs with JSC library built for Android
            url "$rootDir/../node_modules/jsc-android/dist"
        }

        maven { url "https://jitpack.io" }
    }
}


task wrapper(type: Wrapper) {
    gradleVersion = '4.7'
    distributionUrl = distributionUrl.replace("bin", "all")
}

subprojects {
    // fix missing gradle versions in jcenter
    if (project.name.contains('react-native-fast-image')
        || project.name.contains('react-native-image-picker')
        || project.name.contains('react-native-vector-icons')
        ) {
        buildscript {
            repositories {
                maven { url = "https://dl.bintray.com/android/android-tools/" }
            }
        }
    }

    project.configurations.all {
        afterEvaluate {project ->
            if (project.hasProperty("android")) {
                android {
                    compileSdkVersion 26
                    buildToolsVersion '27.0.2'
                }
            }
        }
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'com.android.support'
                && !details.requested.name.contains('multidex') ) {
                details.useVersion "26.0.2"
            }
        }
    }
}